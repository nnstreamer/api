apply plugin: 'com.android.library'

android {
    namespace "org.nnsuite.nnstreamer"
    compileSdk 34
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndkBuild {
                ndkVersion "25.2.9519653"

                def gstRoot

                if (project.hasProperty('gstAndroidRoot'))
                    gstRoot = project.gstAndroidRoot
                else
                    gstRoot = System.env.GSTREAMER_ROOT_ANDROID

                if (gstRoot == null)
                    throw new GradleException('GSTREAMER_ROOT_ANDROID must be set, or "gstAndroidRoot" must be defined in your gradle.properties in the top level directory of the unpacked universal GStreamer Android binaries')

                def nnsRoot

                if (project.hasProperty('nnstreamerRoot'))
                    nnsRoot = project.nnstreamerRoot
                else
                    nnsRoot = System.env.NNSTREAMER_ROOT

                if (nnsRoot == null)
                    throw new GradleException('NNSTREAMER_ROOT must be set, or "nnstreamerRoot" must be defined in your gradle.properties')

                def nnsEdgeRoot

                if (project.hasProperty('nnstreamerEdgeRoot'))
                    nnsEdgeRoot = project.nnstreamerEdgeRoot
                else
                    nnsEdgeRoot = System.env.NNSTREAMER_EDGE_ROOT

                if (nnsEdgeRoot == null)
                    throw new GradleException('NNSTREAMER_EDGE_ROOT must be set, or "nnstreamerEdgeRoot" must be defined in your gradle.properties')

                def mlopsAgentRoot

                if (project.hasProperty('mlopsAgentRoot'))
                    mlopsAgentRoot = project.mlopsAgentRoot
                else
                    mlopsAgentRoot = System.env.MLOPS_AGENT_ROOT

                if (mlopsAgentRoot == null)
                    throw new GradleException('MLOPS_AGENT_ROOT must be set, or "mlopsAgentRoot" must be defined in your gradle.properties')

                def mlApiRoot

                if (project.hasProperty('mlApiRoot'))
                    mlApiRoot = project.mlApiRoot
                else
                    mlApiRoot = System.env.ML_API_ROOT

                if (mlApiRoot == null)
                    throw new GradleException('ML_API_ROOT must be set, or "mlApiRoot" must be defined in your gradle.properties')

                arguments "NDK_PROJECT_PATH=./",
                          "NDK_APPLICATION_MK=src/main/jni/Application.mk",
                          "GSTREAMER_JAVA_SRC_DIR=src/main/java",
                          "GSTREAMER_ROOT_ANDROID=$gstRoot",
                          "GSTREAMER_ASSETS_DIR=src/main/assets",
                          "NNSTREAMER_ROOT=$nnsRoot",
                          "NNSTREAMER_EDGE_ROOT=$nnsEdgeRoot",
                          "MLOPS_AGENT_ROOT=$mlopsAgentRoot",
                          "ML_API_ROOT=$mlApiRoot"

                targets "nnstreamer-native"

                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'

                def tfliteRoot

                if (project.hasProperty('tfliteRoot'))
                    tfliteRoot = project.tfliteRoot
                else
                    tfliteRoot = System.env.TFLITE_ROOT_ANDROID

                if (tfliteRoot != null)
                    arguments "TFLITE_ROOT_ANDROID=$tfliteRoot",
                              "ENABLE_TF_LITE=true"
            }
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    productFlavors {
    }
    sourceSets {
        main {
            if (project.hasProperty('SNPE_EXT_LIBRARY_PATH')) {
                jniLibs.srcDirs += project.properties['SNPE_EXT_LIBRARY_PATH']
                println 'Set jniLibs.srcDirs includes libraries for SNPE'
            }

            if (project.hasProperty('QNN_EXT_LIBRARY_PATH')) {
                jniLibs.srcDirs += project.properties['QNN_EXT_LIBRARY_PATH']
                println 'Set jniLibs.srcDirs includes libraries for QNN'
            }

            if (project.hasProperty('NNFW_EXT_LIBRARY_PATH')) {
                jniLibs.srcDirs += project.properties['NNFW_EXT_LIBRARY_PATH']
                println 'Set jniLibs.srcDirs includes libraries for NNFW'
            }

            if (project.hasProperty('TFLITE_QNN_DELEGATE_EXT_LIBRARY_PATH')) {
                jniLibs.srcDirs += project.properties['TFLITE_QNN_DELEGATE_EXT_LIBRARY_PATH']
                println 'Set jniLibs.srcDirs includes libraries for TFLite QNN Delegate'
            }
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            keepDebugSymbols += "**/*Skel.so"
            keepDebugSymbols += "**/*skel.so"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
